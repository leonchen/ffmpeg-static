PROJECT(ffmpeg_lite_static)

CMAKE_MINIMUM_REQUIRED(VERSION 3.2)

set(POSITION_INDEPENDENT_CODE TRUE)

INCLUDE(ExternalProject)

INCLUDE_DIRECTORIES(BEFORE ${CMAKE_BINARY_DIR}/include)
LINK_DIRECTORIES(${CMAKE_BINARY_DIR}/lib)

SET(ENV{PKG_CONFIG_PATH} ${CMAKE_BINARY_DIR}/lib/pkgconfig)

if(APPLE)
    SET(OPENSSL_ARCH "darwin64-x86_64-cc")
else(APPLE)
    SET(OPENSSL_ARCH "linux-x86_64")
endif(APPLE)

EXTERNALPROJECT_ADD(
  nasm
  URL https://www.nasm.us/pub/nasm/releasebuilds/2.14.02/nasm-2.14.02.tar.bz2
  CONFIGURE_COMMAND ./configure --prefix=${CMAKE_BINARY_DIR}
  BUILD_IN_SOURCE 1
)

EXTERNALPROJECT_ADD(
  gperf
  URL http://ftp.gnu.org/pub/gnu/gperf/gperf-3.1.tar.gz
  CONFIGURE_COMMAND ./configure --prefix=${CMAKE_BINARY_DIR} --enable-pic
  BUILD_IN_SOURCE 1
)

EXTERNALPROJECT_ADD(
  zlib
  URL https://github.com/madler/zlib/archive/v1.2.11.tar.gz
  CONFIGURE_COMMAND ./configure --prefix=${CMAKE_BINARY_DIR} --static
  BUILD_IN_SOURCE 1
)

EXTERNALPROJECT_ADD(
  libpng
  DEPENDS zlib
  URL https://download.sourceforge.net/libpng/libpng-1.6.36.tar.gz
  CONFIGURE_COMMAND CFLAGS=-I${CMAKE_BINARY_DIR}/include CPPFLAGS=-I${CMAKE_BINARY_DIR}/include LDFLAGS=-L${CMAKE_BINARY_DIR}/lib ./configure --prefix=${CMAKE_BINARY_DIR} --disable-shared --enable-static
  BUILD_IN_SOURCE 1
)

EXTERNALPROJECT_ADD(
  x264
  DEPENDS nasm
  GIT_REPOSITORY http://git.videolan.org/git/x264.git
  GIT_TAG 545de2ffec6ae9a80738de1b2c8cf820249a2530
  CONFIGURE_COMMAND ./configure --prefix=${CMAKE_BINARY_DIR} --bit-depth=8 --chroma-format=all --enable-static --enable-pic --enable-strip --disable-cli --disable-gpl --disable-avs --disable-ffms --disable-swscale --disable-gpac --disable-lsmash
  BUILD_COMMAND make
  BUILD_ALWAYS 0
  BUILD_IN_SOURCE 1
)

EXTERNALPROJECT_ADD(
  libopenjp2
  URL https://github.com/uclouvain/openjpeg/releases/download/v2.3.0/openjpeg-v2.3.0-linux-x86_64.tar.gz
  CONFIGURE_COMMAND sh -c "rm -rf ./build && mkdir build && cd ./build && cmake -DCMAKE_BUILD_TYPE=Release .."
  BUILD_COMMAND sh -c "cd ./build && make"
  INSTALL_COMMAND sh -c "cd ./build && make DESTDIR=${CMAKE_BINARY_DIR} install"
  BUILD_IN_SOURCE 1
)

SET(FFMPEG_COMPONENTS --disable-doc --disable-postproc --disable-debug --disable-indevs --disable-outdevs --disable-sdl2 --disable-lzma --disable-iconv)
LIST(APPEND FFMPEG_COMPONENTS --enable-static --enable-gpl --enable-pic --enable-libx264 --enable-ffmpeg --enable-ffprobe --disable-ffplay --disable-hwaccels --disable-v4l2-m2m --disable-vaapi --disable-vdpau --disable-videotoolbox --disable-audiotoolbox)
LIST(APPEND FFMPEG_COMPONENTS --enable-avcodec --enable-avformat --enable-avfilter --enable-swresample --enable-swscale --enable-avutil --enable-libopenjpeg)
LIST(APPEND FFMPEG_COMPONENTS --disable-demuxers --enable-demuxer=mov,mpegts,h264,hevc,aac,rawvideo,concat)
LIST(APPEND FFMPEG_COMPONENTS --disable-muxers --enable-muxer=mp4,mov,mpegts,dash,hls,segment,tee,h264,hevc,aac,rawvideo)
LIST(APPEND FFMPEG_COMPONENTS --disable-decoders --enable-decoder=h264,hevc,aac)
LIST(APPEND FFMPEG_COMPONENTS --disable-encoders --enable-encoder=libx264,aac,png)
LIST(APPEND FFMPEG_COMPONENTS --disable-filters --enable-filter=scale,copy,concat,split,setpts,bwdif)
LIST(APPEND FFMPEG_COMPONENTS --disable-protocols --enable-protocol=file,pipe,unix,concat)
LIST(APPEND FFMPEG_COMPONENTS --extra-cflags=-I${CMAKE_BINARY_DIR}/include --extra-ldflags=-L${CMAKE_BINARY_DIR}/lib --extra-libs=-lpng\ -lz\ -lstdc++\ -lm\ -ldl\ -lopenjp2)

EXTERNALPROJECT_ADD(
  ffmpeg
  DEPENDS nasm gperf x264 libpng
  URL http://www.ffmpeg.org/releases/ffmpeg-4.1.tar.bz2
  CONFIGURE_COMMAND PKG_CONFIG_PATH=$ENV{PKG_CONFIG_PATH} ./configure --prefix=${CMAKE_BINARY_DIR} --datadir=${CMAKE_BINARY_DIR}/etc ${FFMPEG_COMPONENTS}
  BUILD_COMMAND make
  BUILD_ALWAYS 0
  BUILD_IN_SOURCE 1
)
